buildscript {
  repositories {
    mavenCentral()
    maven { url 'https://www.jetbrains.com/intellij-repository/snapshots' }
  }
}

plugins {
  id "org.jetbrains.intellij" version "1.17.4"
  id "checkstyle"
  id 'org.asciidoctor.jvm.convert' version '4.0.4'
  // once windows classpaths get too long, use this dependency to make them shorter again
  id "com.github.ManifestClasspath" version "0.1.0-RELEASE"
  id 'net.ltgt.errorprone' version '4.3.0' apply false
}

checkstyle {
  toolVersion '8.30'
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'net.ltgt.errorprone'

import org.jsoup.Jsoup
import org.jetbrains.intellij.tasks.RunPluginVerifierTask.FailureLevel

allprojects {
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

intellij {
  version = 'IC-2024.1.6'
  pluginName = 'asciidoclet-intellij-plugin'
  updateSinceUntilBuild = false
  plugins = [
    'PsiViewer:241.14494.158-EAP-SNAPSHOT', // used for debugging
    'org.asciidoctor.intellij.asciidoc:0.42.0', // used as a base for rendering AsciiDoc
    'com.intellij.java' // used to override Java
  ]
}

asciidoctor {
  baseDir
  sourceDir file('.')
  sources {
    include 'CHANGELOG.adoc'
  }
  outputDir file('build/docs')
}

version = System.getenv('VERSION')

patchPluginXml {
  dependsOn = [ asciidoctor ]
  changeNotes = provider {
    Jsoup.parse(file('build/docs/CHANGELOG.html').getText('UTF-8'))
      .select("#releasenotes").get(0).nextElementSibling().children()
      // .subList(0, 20)
      .stream().map { e ->
      e.html()
        .replaceAll('\\(preview, available from GitHub releases\\)', '')
        .replaceAll('#([0-9]+)', '<a href="https://github.com/ahus1/asciidoclet-intellij-plugin/issues/$1">#$1</a>')
        // regex for GitHub user names from https://github.com/shinnn/github-username-regex
        .replaceAll('(?i)@([a-z\\d](?:[a-z\\d]|-(?=[a-z\\d])){0,38})', '<a href="https://github.com/$1">@$1</a>')
    }
      .collect().join("\n")
  }
  pluginDescription = "${file('src/main/resources/META-INF/description.html').getText('UTF-8')}"
}

runIde {
  jvmArgs = ['-Xmx1024m','-XX:+UnlockDiagnosticVMOptions']

  // should not automatically reload plugin on change in IDE, as JRuby is not very good at this
  systemProperty('idea.auto.reload.plugins', 'false')

  systemProperty('ide.plugins.snapshot.on.unload.fail', 'true')
}

runPluginVerifier {
  // https://github.com/JetBrains/gradle-intellij-plugin/blob/master/src/main/kotlin/org/jetbrains/intellij/tasks/RunPluginVerifierTask.kt#L485
  failureLevel = [FailureLevel.INVALID_PLUGIN, FailureLevel.COMPATIBILITY_PROBLEMS, FailureLevel.NOT_DYNAMIC]
  ideVersions = Arrays.asList(project.properties['pluginVerifierIdeVersions'].toString().split(','))
  freeArgs = ["-mute", "TemplateWordInPluginId"]
}

publishPlugin {
  token = System.getenv('PUBLISH_TOKEN')
  channels = [("true" == System.getenv("PRE_RELEASE")) ? 'eap' : 'default']
}

test {
  testLogging {
    // this show the full exception on failed tests on travis
    exceptionFormat = 'full'
  }
}

dependencies {
  errorprone 'com.google.errorprone:error_prone_core:2.41.0'
  testImplementation 'junit:junit:4.13.2'
}

sourceSets {
  main {
    resources {
      exclude('META-INF/description.html')
    }
  }
}

java {
  targetCompatibility = "11"
  sourceCompatibility = "11"
}

compileJava {
  options.errorprone.error('StreamResourceLeak') // enforce errors where there would be warning in the standard only
}
